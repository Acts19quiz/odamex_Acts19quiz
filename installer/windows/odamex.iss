; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=Odamex
AppVersion=0.8.3
AppVerName=Odamex 0.8.3
AppPublisher=Odamex Development Team
AppPublisherURL=https://odamex.net
AppSupportURL=https://odamex.net
AppUpdatesURL=https://odamex.net
VersionInfoVersion=0.8.3
VersionInfoProductVersion=0.8.3
VersionInfoProductName=Odamex Windows Installer
DefaultDirName={userpf}\odamex
DefaultGroupName=Odamex
AllowNoIcons=true
LicenseFile=..\..\LICENSE
;InfoBeforeFile=..\..\CHANGES
OutputBaseFilename=odamex-win-0.8.3
Compression=lzma2
SolidCompression=true
AlwaysShowDirOnReadyPage=true
ChangesEnvironment=true
AppID={{2E517BBB-916F-4AB6-80E0-D4A292513F7A}
PrivilegesRequired=none
ShowLanguageDialog=auto
UninstallDisplayIcon={app}\odamex.exe
VersionInfoCompany=Odamex
EnableDirDoesntExistWarning=true
DirExistsWarning=no
MinVersion=0,6.0
AllowRootDirectory=True
ChangesAssociations=Yes
ArchitecturesInstallIn64BitMode=x64
UninstallDisplaySize=21197782
UsePreviousAppDir=yes
;DisableDirPage=auto
;DisableProgramGroupPage=auto
;AppModifyPath={app}\UninsHs.exe /m0=Odamex
WizardImageFile=..\..\media\wininstall_largeback.bmp
WizardSmallImageFile=..\..\media\wininstall_wizardicon.bmp

[Languages]
Name: english; MessagesFile: compiler:Default.isl
Name: french; MessagesFile: compiler:Languages\French.isl
Name: german; MessagesFile: compiler:Languages\German.isl
Name: spanish; MessagesFile: compiler:Languages\Spanish.isl
Name: en; MessagesFile: compiler:Default.isl
Name: fr; MessagesFile: compiler:Languages\French.isl
Name: de; MessagesFile: compiler:Languages\German.isl
Name: es; MessagesFile: compiler:Languages\Spanish.isl

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked

[Types]
Name: full; Description: Full installation
Name: compact; Description: Client-only installation
Name: custom; Description: Custom installation; Flags: iscustom

[Components]
Name: base; Description: Base data; Types: full compact custom; Flags: fixed
Name: client; Description: Odamex Client; Types: full compact custom; Flags: DisableNoUninstallWarning
Name: server; Description: Odamex Server; Types: full; Flags: DisableNoUninstallWarning
Name: launcher; Description: Odalaunch (Game Launcher); Types: full compact custom; Flags: DisableNoUninstallWarning

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; COMMON FILES
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
Source: Build64\libraries\SDL2-2.0.12\COPYING.txt; DestDir: {app}\licenses; DestName: COPYING.SDL2.txt;Flags: ignoreversion; Components: base
Source: Build64\libraries\SDL2_mixer-2.0.4\COPYING.txt; DestDir: {app}\licenses; DestName: COPYING.SDL2_mixer.txt;Flags: ignoreversion; Components: base
Source: Build64\libraries\SDL2_mixer-2.0.4\lib\x64\LICENSE.FLAC.txt; DestDir: {app}\licenses; Flags: ignoreversion; Components: base
Source: Build64\libraries\SDL2_mixer-2.0.4\lib\x64\LICENSE.modplug.txt; DestDir: {app}\licenses; Flags: ignoreversion; Components: base
Source: Build64\libraries\SDL2_mixer-2.0.4\lib\x64\LICENSE.mpg123.txt; DestDir: {app}\licenses; Flags: ignoreversion; Components: base
Source: Build64\libraries\SDL2_mixer-2.0.4\lib\x64\LICENSE.ogg-vorbis.txt; DestDir: {app}\licenses; Flags: ignoreversion; Components: base
Source: Build64\libraries\SDL2_mixer-2.0.4\lib\x64\LICENSE.opus.txt; DestDir: {app}\licenses; Flags: ignoreversion; Components: base
Source: Build64\libraries\SDL2_mixer-2.0.4\lib\x64\LICENSE.opusfile.txt; DestDir: {app}\licenses; Flags: ignoreversion; Components: base
Source: ..\..\3RD-PARTY-LICENSES; DestDir: {app}; DestName: 3RD-PARTY-LICENSES.txt;Flags: ignoreversion; Components: base
Source: ..\..\CHANGELOG; DestDir: {app}; DestName: CHANGELOG.txt; Flags: ignoreversion; Components: base
Source: ..\..\libraries\curl\COPYING; DestDir: {app}\licenses; DestName: COPYING.curl.txt; Flags: ignoreversion; Components: base
Source: ..\..\libraries\libminiupnpc\LICENSE; DestDir: {app}\licenses; DestName: LICENSE.libminiupnpc.txt; Flags: ignoreversion; Components: base
Source: ..\..\libraries\libpng\LICENSE; DestDir: {app}\licenses; DestName: LICENSE.libpng.txt; Flags: ignoreversion; Components: base
Source: ..\..\libraries\portmidi\license.txt; DestDir: {app}\licenses; DestName: license.portmidi.txt; Flags: ignoreversion; Components: base
Source: ..\..\LICENSE; DestDir: {app}; Flags: ignoreversion; Components: base
Source: ..\..\MAINTAINERS; DestDir: {app}; Flags: ignoreversion; Components: base

Source: ..\..\config-samples\*; DestDir: {app}\config-samples; Flags: ignoreversion; Components: server
Source: ..\..\wad\odamex.wad; DestDir: {app}; Flags: ignoreversion; Components: client server

; Source: "UninsHs.exe"; DestDir: "{app}"; Flags: restartreplace

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 64-BIT FILES
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
Source: Build64\client\RelWithDebInfo\libFLAC-8.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build64\client\RelWithDebInfo\libmodplug-1.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build64\client\RelWithDebInfo\libmpg123-0.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build64\client\RelWithDebInfo\libogg-0.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build64\client\RelWithDebInfo\libopus-0.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build64\client\RelWithDebInfo\libvorbis-0.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build64\client\RelWithDebInfo\libvorbisfile-3.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build64\client\RelWithDebInfo\odamex.exe; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build64\client\RelWithDebInfo\odamex.pdb; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build64\client\RelWithDebInfo\SDL2_mixer.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build64\client\RelWithDebInfo\SDL2.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode

Source: Build64\server\RelWithDebInfo\odasrv.exe; DestDir: {app}; Flags: ignoreversion; Components: server; Check: Is64BitInstallMode
Source: Build64\server\RelWithDebInfo\odasrv.pdb; DestDir: {app}; Flags: ignoreversion; Components: server; Check: Is64BitInstallMode

Source: Build64\odalaunch\RelWithDebInfo\odalaunch.exe; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: Build64\odalaunch\RelWithDebInfo\odalaunch.pdb; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: Build64\odalaunch\RelWithDebInfo\wxbase314u_net_vc14x_x64.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: Build64\odalaunch\RelWithDebInfo\wxbase314u_vc14x_x64.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: Build64\odalaunch\RelWithDebInfo\wxbase314u_xml_vc14x_x64.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: Build64\odalaunch\RelWithDebInfo\wxmsw314u_core_vc14x_x64.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: Build64\odalaunch\RelWithDebInfo\wxmsw314u_html_vc14x_x64.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: Build64\odalaunch\RelWithDebInfo\wxmsw314u_xrc_vc14x_x64.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 32-BIT FILES
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
Source: Build32\client\RelWithDebInfo\libFLAC-8.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build32\client\RelWithDebInfo\libmodplug-1.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build32\client\RelWithDebInfo\libmpg123-0.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build32\client\RelWithDebInfo\libogg-0.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build32\client\RelWithDebInfo\libopus-0.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build32\client\RelWithDebInfo\libvorbis-0.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build32\client\RelWithDebInfo\libvorbisfile-3.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build32\client\RelWithDebInfo\odamex.exe; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build32\client\RelWithDebInfo\odamex.pdb; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build32\client\RelWithDebInfo\SDL2_mixer.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: Build32\client\RelWithDebInfo\SDL2.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode

Source: Build32\server\RelWithDebInfo\odasrv.exe; DestDir: {app}; Flags: ignoreversion; Components: server; Check: Is64BitInstallMode
Source: Build32\server\RelWithDebInfo\odasrv.pdb; DestDir: {app}; Flags: ignoreversion; Components: server; Check: Is64BitInstallMode

Source: Build32\odalaunch\RelWithDebInfo\odalaunch.exe; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: Build32\odalaunch\RelWithDebInfo\odalaunch.pdb; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: Build32\odalaunch\RelWithDebInfo\wxbase314u_net_vc14x.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: Build32\odalaunch\RelWithDebInfo\wxbase314u_vc14x.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: Build32\odalaunch\RelWithDebInfo\wxbase314u_xml_vc14x.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: Build32\odalaunch\RelWithDebInfo\wxmsw314u_core_vc14x.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: Build32\odalaunch\RelWithDebInfo\wxmsw314u_html_vc14x.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: Build32\odalaunch\RelWithDebInfo\wxmsw314u_xrc_vc14x.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode

[Dirs]
;Name: "{localappdata}\odamex"; Flags: uninsalwaysuninstall

[INI]
Filename: {app}\Odamex Website.url; Section: InternetShortcut; Key: URL; String: https://odamex.net
Filename: {app}\Releases Changelog.url; Section: InternetShortcut; Key: URL; String: https://odamex.net/wiki/Releases

[Icons]
Name: {group}\Odamex Client; Filename: {app}\odamex.exe; WorkingDir: {app}
Name: {group}\Odalaunch; Filename: {app}\odalaunch.exe; WorkingDir: {app}
Name: {group}\Odamex Server; Filename: {app}\odasrv.exe; WorkingDir: {app}
Name: {group}\{cm:ProgramOnTheWeb,Odamex}; Filename: {app}\Odamex Website.url
Name: {group}\Releases Changelog; Filename: {app}\Releases Changelog.url
Name: {group}\{cm:UninstallProgram,Odamex}; Filename: {uninstallexe}
;Name: "{group}\{cm:UninstallProgram,Odamex}"; Filename: "{app}\UninsHs.exe"; Parameters: /u3=Odamex; WorkingDir: {app}
Name: {userdesktop}\Odamex Launcher; Filename: {app}\odalaunch.exe; Tasks: desktopicon; WorkingDir: {app}; IconIndex: 0; Components: launcher

[Run]
Filename: {app}\odalaunch.exe; Description: {cm:LaunchProgram,Odalaunch}; Flags: nowait postinstall skipifsilent
;Filename: {app}\UninsHs.exe; Parameters: /r={{2E517BBB-916F-4AB6-80E0-D4A292513F7A},{language},{srcexe}; Flags: runminimized
;Filename: {app}\UninsHs.exe; Parameters: /r=Odamex,{language},{srcexe},{localappdata}\odamex\setup.exe
;Flags: nowait runhidden runminimized


[UninstallDelete]
Type: files; Name: {app}\Odamex Website.url
Type: files; Name: {app}\Releases Changelog.url
Type: files; Name: {app}\odamex.out
Type: files; Name: {app}\odamex.cfg
Type: files; Name: {app}\odasrv.cfg
Type: files; Name: {app}\*.log
;Type: filesandordirs; Name: "{localappdata}\odamex"
Type: dirifempty; Name: {app}

[Registry]
Root: HKCR; Subkey: odamex; ValueType: string; ValueData: URL:Odamex Protocol; Flags: uninsdeletekey noerror
Root: HKCR; Subkey: odamex; ValueType: string; ValueName: Url Protocol; Flags: createvalueifdoesntexist uninsdeletekey noerror
Root: HKCR; Subkey: odamex\DefaultIcon; ValueData: odamex.exe,1; Flags: createvalueifdoesntexist uninsdeletekey noerror
Root: HKCR; Subkey: odamex\shell\open\command; ValueData: """{app}\odamex.exe"" ""%1"""; Flags: createvalueifdoesntexist uninsdeletekey noerror; ValueType: string
Root: HKCR; SubKey: .odd; ValueType: string; ValueData: Odamex Data Demo; Flags: uninsdeletekey
Root: HKCR; SubKey: Odamex Data Demo; ValueType: string; ValueData: Odamex Game Demo Format; Flags: uninsdeletekey
Root: HKCR; SubKey: Odamex Data Demo\Shell\Open\Command; ValueType: string; ValueData: """{app}\odamex.exe"" ""%1"""; Flags: uninsdeletekey
Root: HKCR; Subkey: Odamex Data Demo\DefaultIcon; ValueType: string; ValueData: {app}\odamex.exe,1; Flags: uninsdeletevalue

[Code]
function ShouldSkipPage(CurPage: Integer): Boolean;
begin
  if Pos('/SP-', UpperCase(GetCmdTail)) > 0 then
    case CurPage of
      wpLicense, wpPassword, wpInfoBefore, wpUserInfo,
      wpSelectDir, wpSelectProgramGroup, wpInfoAfter:
        Result := True;
    end;
end;

function IsUpgrade(): Boolean;
var
   sPrevPath: String;
begin
  sPrevPath := WizardForm.PrevAppDir;
  Result := (sPrevPath <> '');
end;

const
  WM_LBUTTONDOWN = 513;
  WM_LBUTTONUP = 514;

procedure InitializeWizard();
begin
  if (Pos('/SP-', UpperCase(GetCmdTail)) > 0) then
  begin
    PostMessage(WizardForm.NextButton.Handle,WM_LBUTTONDOWN,0,0);
    PostMessage(WizardForm.NextButton.Handle,WM_LBUTTONUP,0,0);
  end;
end;

procedure CurPageChanged(CurPageID: Integer);
begin
  if (Pos('/SP-', UpperCase(GetCmdTail)) > 0) and
    (CurPageID = wpSelectComponents) then
    WizardForm.BackButton.Visible := False;
end;
